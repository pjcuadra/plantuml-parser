start: "@startuml" (skinparam | class | relationship)* "@enduml"

// Skinparam
skinparam: "skinparam" sparam svalue

// Classes
class: "class" class_name stereotype? ("{" (class_group_title? (attribute | method) class_group_title?)* "}")?
     | class_name ":" (attribute | method)?

// Stereotype
stereotype: "<<" stereotype_name ">>"

// Sections with title
class_group_title: ".." (group_title "..")?
                 | "--" (group_title "--")?
                 | "__" (group_title "__")?
                 | "=="

// Variables
variable: var ":" type
        | var ":" type "[]"
        | var ":" type "*"
        | type var
        | type "[]" var
        | type "*" var
        | type
        | var

text_field: "{field}" field_text

?attribute: att_scope? visibility? variable
          | visibility? att_scope? variable
          | att_scope? visibility? text_field
          | visibility? att_scope? text_field

param_list: [variable ("," variable)*]

function: method_name "(" param_list ")" 
        | method_name "("param_list")" ":" type
        | type method_name "(" param_list ")"

text_method: "{method}" method_text

method: att_scope? visibility? function
      | visibility? att_scope? function
      | att_scope? visibility? text_method
      | visibility? att_scope? text_method

// Attribute Scope
att_scope: "{static}" -> static
         | "{abstract}" -> abstract

// Relationships
relationship: ( relationship_class | relationship_state) 

relationship_class: arrow_end_1 cardinality? arrow_head_1 arrow_body arrow_head_2 cardinality? arrow_end_2 [":" relation_text]

// Statemachine
relationship_state: state
                  | substate

substate: "state" arrow_end_1 "{" state* "}"

state: initial_state arrow_body ">" arrow_end_2
     | arrow_end_1 arrow_body ">" arrow_end_2 [":" relation_text]
     | arrow_end_1 arrow_body ">" final_state
     | arrow_end_1 [":" relation_text]
     | substate

// Arrow Heads
!arrow_head_1: "<"
             | "<|"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_head_2: ">"
             | "|>"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_body: ("-"
           | "--"
           | "---"
           | "-left-"
           | "-up-"
           | "-right-"
           | "-down-"
           | "."
           | ".."
           | ".left."
           | ".up."
           | ".right."
           | ".down." )

cardinality: ESCAPED_STRING

// Variables and Methods visibility
visibility: "+" -> public
          | "-" -> private
          | "#" -> protected
          | "~" -> package

initial_state: final_state
final_state: "[*]"
arrow_end_1: CNAME
arrow_end_2: CNAME
type: CNAME
var: CNAME
sparam: CNAME
svalue: CNAME|WORD|ESCAPED_STRING|DIGIT
attribute_name: CNAME
method_name: CNAME
class_name: CNAME
stereotype_name: CNAME
relation_text: (WORD|ESCAPED_STRING|DIGIT|">"|"<")*
field_text: (WORD|ESCAPED_STRING|DIGIT)*
method_text: (WORD|ESCAPED_STRING|DIGIT)*
class_mem_text: (WORD|ESCAPED_STRING|DIGIT)*
group_title: (WORD|ESCAPED_STRING|DIGIT)*

%import common.CNAME
%import common.ESCAPED_STRING
%import common.WORD
%import common.DIGIT
%import common.WS
%ignore WS
